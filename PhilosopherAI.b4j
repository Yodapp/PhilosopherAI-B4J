AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
File2=philosopher-square-Close.png
FileGroup1=Default Group
FileGroup2=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=jokhttputils2
Library4=json
Library5=jxui
Library6=okhttp
NumberOfFiles=2
NumberOfLibraries=6
NumberOfModules=0
Version=9.1
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private xui As XUI 
	Private lblCurrentTemp As Label
	Private lblCurrentTopP As Label
	Private lblKey As Label
	Private txtApiKey As TextField
	Private txtTopic As TextArea
	Private btnAskPhilosopher As Button
	Private sliderTemp As Slider
	Private sliderTopP As Slider
	Private temperature As Double
	Private topP As Double
	Private lblThinking As Label
	Private lblAnswer As Label
	Private scroll As ScrollPane
	Private imgPhilosopher As ImageView
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	lblCurrentTemp.Text = "0.5"
	lblCurrentTopP.Text = "0.5"
	imgPhilosopher.Visible = False
	scroll.Visible = False
	scroll.InnerNode = lblAnswer
	MainForm.Show
End Sub

Private Sub btnAskPhilosopher_Click
	toggleViews(True)
	
	Dim result = PostRequestToOpenAI As ResumableSub
	Wait For(result) complete (complete As String)
	
	Dim answer As String
	answer = ParseResponse(complete)
	
	Dim output As StringBuilder
	output.Initialize
	lblAnswer.Text = answer
	
	toggleViews(False)
End Sub

Private Sub toggleViews(isThinking As Boolean)
	If isThinking Then
		scroll.Visible = False
		imgPhilosopher.Visible = True
		btnAskPhilosopher.Enabled = False
		lblAnswer.Visible = False
	Else
		scroll.Visible = True
		imgPhilosopher.Visible = False
		btnAskPhilosopher.Enabled = True
		lblAnswer.Visible = True
	End If
End Sub

Private Sub sliderTemp_ValueChange (Value As Double)
	lblCurrentTemp.Text = Round2(Value, 2)
	temperature = Round2(Value, 2)
End Sub

Private Sub sliderTopP_ValueChange (Value As Double)
	lblCurrentTopP.Text = Round2(Value, 2)
	topP = Round2(Value, 2)
End Sub

Private Sub PostRequestToOpenAI() As ResumableSub
	Dim answer As String
	Dim http As HttpJob
	Dim key As String
	Dim data As String
	key = GetApiKey
	data = GetJson
	http.Initialize("", Me)
	http.PostString("https://api.openai.com/v1/engines/davinci/completions", data)
	http.GetRequest.SetHeader("Authorization", key)
	http.GetRequest.SetContentType("application/json")
	http.GetRequest.SetHeader("Accept","application/json")
	Wait For (http) JobDone(http As HttpJob)
	If http.Success Then
		answer = http.GetString
	Else
		answer = http.ErrorMessage
	End If
	http.Release
	Return answer
End Sub

Private Sub ParseResponse(response As String) As String
	Dim json As JSONParser
	json.Initialize(response)
	Dim values As Map
	values = json.NextObject
	Dim choices As List = values.Get("choices")
	For Each choice As Map In choices
		Dim text As String = choice.Get("text")
	Next
	Return text
End Sub

private Sub GetApiKey() As String
	Dim str As StringBuilder
	str.Initialize
	str.Append("Bearer").Append(" ").Append(txtApiKey.Text)
	Return str.ToString
End Sub

Private Sub GetJson() As String
	Dim json As JSONGenerator
	Dim values As Map
	Dim topic = GetTopic As String
	values.Initialize
	values.Put("prompt", topic)
	values.Put("temperature", temperature)
	values.Put("top_p", topP)
	values.Put("presence_penalty", 1)
	values.Put("frequency_penalty", 1)
	values.Put("max_tokens", 500)
	values.Put("stop", "['''Philosopher AI:', '\n\n\n']")
	json.Initialize(values)
	Return json.ToString
End Sub

Private Sub GetTopic() As String
	Dim str As StringBuilder
	str.Initialize
	str.Append("Below is a long paragraph generated by a philosopher AI, which sees the human world from the outside, without the prejudices of human experience. Fully neutral and objective, the AI sees the world as is. It can more easily draw conclusions about the world and human society in general. ")
	str.Append("The topic provided by the human Is '")
	str.Append(txtTopic.Text)
	str.Append("' , to which the AI responds with deep thought.")
	str.Append("Philosopher AI: 'Hmmm, interesting topic. Here is my rather lengthy response:'")
	Dim topic = str.ToString() As String
	Return topic
End Sub